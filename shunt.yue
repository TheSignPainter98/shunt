local *

import 'shunt.compat' as :apply_compat, :HOST, :test_compat
apply_compat!

import 'shunt.quicktype' as :declare_type

import 'shunt.args' as :Args
import 'shunt.instrument' as :instrument
import 'shunt.logger' as :log
import 'shunt.monitor' as :detect_and_use_monitor
import 'shunt.quicktype' as :F
import 'shunt.spec' as :run_tests

import 'shunt.cmd.clean'
import 'shunt.cmd.declare'
import 'shunt.cmd.disable'
import 'shunt.cmd.edit'
import 'shunt.cmd.enable'
import 'shunt.cmd.flash'
import 'shunt.cmd.init'
import 'shunt.cmd.start'
import 'shunt.cmd.upgrade'
import 'shunt.logger'
import 'shunt.spec'

-- Import type definitions
require 'shunt.component'

debug_ = require 'shunt.cmd.debug'

global skip_minecraft_tests = false
-- Some tests have unsuppressible error output in valid cases. This is a hack
-- to avoid mental false-positives by users.
global run_ugly_tests = false

force_error_on_bad_exit = false

main = (raw_args) ->
  args, ok = Args::parse raw_args
  if not ok
    return

  logger.set_log_verbosity not args.terse
  if args.test?
    logger.activate_test_mode!
    spec.set_log_verbosity args.test.verbose

  force_error_on_bad_exit = args.force_error_on_bad_exit

  if args.instrument
    instrument -> run args
  else
    run args

run = (args) ->
  if args.test?
    run_ugly_tests = args.test.run_ugly_tests
    skip_minecraft_tests = args.test.no_minecraft
    ok = run_tests args.test.filter
    if not ok
      os.exit 1
  else if args.start?
    if not args.ignore_monitor
      detect_and_use_monitor!
    start.main args.start
  else if args.init?
    init.main args.init
  else if args.clean?
    clean.main args.clean
  else if args.enable?
    enable.main args.enable
  else if args.disable?
    disable.main args.disable
  else if args.edit?
    edit.main args.edit
  else if args.flash?
    flash.main args.flash
  else if args.declare?
    declare.main args.declare
  else if args.upgrade?
    upgrade.main args.upgrade
  else if args.debug?
    debug_.main args.debug
  else
    error 'internal error: no command recognised'

spec.spec ->
  import 'shunt.spec' as :describe, :it

  describe 'compat', ->
    it 'passes checks', ->
      test_compat!

args = {...}
ok = true
try
  main args
catch err
  if err != 'EXIT(0)'
    if HOST == 'native'
      io.stderr\write debug.traceback err
    else
      print debug.traceback err
    ok = false
switch HOST
  when 'native'
    if not ok
      os.exit 1
  when 'minecraft'
    if not ok and force_error_on_bad_exit
      os.exit 1
  else
    error "internal error: unrecognised host #{HOST}"
