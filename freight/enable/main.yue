local *

import Flag, Subcommand from require 'clap'
import F from require 'quicktype'

export subcommand = with Subcommand 'enable'
  \description 'enable freight on startup'
  \add with Flag 'force'
    \description 'overwrite existing config'
  \add with Flag 'start'
    \short nil
    \description 'start running immediately'

export main = F '({}) -> <>', (args) ->
  if not args.force
    with? io.open 'startup.lua', 'r'
      \close!
      print 'cannot enable freight: existing startup script detected\nre-run with --force to overwrite'
      return

  with assert io.open 'startup.lua', 'w+'
    assert \write _startup_script
    assert \close!

  if args.start
    os.reboot?!
  return

export _startup_script = [[
shell.run('set motd.enable false')
shell.run('freight start')
]]
