local *

import 'shunt.quicktype' as :declare_type, :F
import 'shunt.spec' as :spec

declare_type 'Random', [[{
  random: (number) => number,
  pick: ([some]) => ?some,
  shuffle: ([some]) => <>,
}]]
export class PseudoRandom
  new: F '(?number) => <>', (@state=math.random 2147483647) =>

  random: F '(number) => number', (max) =>
    @state = (47328190 * @state + 1000003) % 2147483647
    1 + @state % max -- Return value is in the range [1, max]

  pick: F '([some]) => ?some', (options) =>
    if #options == 0
      return nil
    options[@random #options]

  shuffle: F '([some]) => <>', (list) =>
    n = #list
    for i = 1, n
      new_index = @random n
      list[i], list[new_index] = list[new_index], list[i]

spec ->
  import 'shunt.spec_macros' as $

  import 'shunt.spec' as :describe, :it, :matchers

  import contains_value, deep_eq, eq, ge, gt, le from matchers

  describe 'PseudoRandom', ->
    describe '\\random', ->
      it 'returns expected values', ->
        with PseudoRandom!
          ATTEMPTS = 100
          MAX = 5
          values = {}
          for _ = 1, ATTEMPTS
            values[] = \random MAX

          max = -1
          min = 100 * MAX
          for value in *values
            if max < value
              max = value
            if min > value
              min = value
          $expect_that min, ge 1
          $expect_that max, le MAX

      it 'returns different values', ->
        with PseudoRandom!
          ATTEMPTS = 100
          MAX = 5
          seen = {}
          num_unique = 0
          for _ = 1, ATTEMPTS
            value = \random MAX
            if seen[value]
              continue
            num_unique += 1

          $expect_that num_unique, gt 1

      it 'is reentrant', ->
        SEED = 12345
        MAX = 1000000
        rand1 = PseudoRandom SEED
        rand2 = PseudoRandom SEED

        values1 = {}
        values2 = {}
        for _ = 1, 100
          values1[] = rand1\random MAX
          values2[] = rand2\random MAX

        $expect_that values1, deep_eq values2

    describe '\\pick', ->
      it 'returns nil on empty input', ->
        $expect_that (PseudoRandom!\pick {}), eq nil

      it 'selects a present element', ->
        $expect_that (PseudoRandom!\pick {'a'}), eq 'a'

      it 'selects different elements', ->
        elems = [ tostring i for i = 1, 100 ]
        seen = {}
        unique_elems = {}
        rand = PseudoRandom!
        for i = 1, #elems
          elem = rand\pick elems
          if not seen[elem]
            unique_elems[] = elem
          seen[elem] = true

        $expect_that #unique_elems, gt 1

    describe '\\shuffle', ->
      it 'retains all elements', ->
        elems = {'a', 'b', 'c', 'd'}
        elems2 = { ...elems }
        PseudoRandom!\shuffle elems2
        for elem in *elems
          $expect_that elems2, contains_value eq elem
